package examples;

import ergoline::_;
import ck;

class hello {
    val previous: ck::future<int>;
    val next: ck::future<int>;

    @entry def hello(=previous: ck::future<int>, =next: ck::future<int>) {
        self@wait();
    }

    @threaded @entry def wait() {
        val i = await previous;
        println(`hello #${i}`);
        next.set(i + 1);
    }
}

@main class main {
    val n: int;

    @entry def main(args: array<string>) {
       n = (args.size() > 1) ? args[1].toInt() : 16;
       val first = ck::future<int>();
       var curr = first;
       // chain n futures with first at the head
       for (i <- (0 to n)) {
           val next = ck::future<int>();
           // create a hello chare for each
           new hello@(curr, next);
           curr = next;
       }
       // set the first future
       first.set(0);
       // then we wait on the last
       self@wait(curr);
    }

    @threaded @entry def wait(f: ck::future<int>) {
        val i = await f;
        assert(i == n);
        println(`hello #${i}`);
        exit();
    }
}
