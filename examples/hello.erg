package examples;

import ergoline::_;
import ck;

class hello {
    val selfProxy: hello@;
    val previous: ck::future<int>;
    val next: ck::future<int>;

    @entry def hello(=selfProxy: hello@, =previous: ck::future<int>, =next: ck::future<int>) {
        selfProxy.wait();
    }

    @threaded @entry def wait() {
        val i = previous.get();
        println("hello #" + i.toString());
        next.set(i + 1);
    }
}

@main class main {
    @entry def main(selfProxy: main@, args: array<string>): unit {
       val n = (args.size() > 1) ? args[1].toInt() : 16;
       val first = ck::future<int>();
       var curr: ck::future<int> = first;
       for (var i: int = 0; i < n; i += 1) {
           val next = ck::future<int>();
           new hello@(curr, next);
           curr = next;
       }
       first.set(0);
       selfProxy.wait(curr);
    }

    @threaded @entry def wait(f: ck::future<int>) {
        val i = f.get();
        println("hello #" + i.toString());
        exit();
    }
}
