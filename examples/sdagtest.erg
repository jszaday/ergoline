package examples;

import ergoline::_;

@main class main {
    var count: int = 0;
    val n: int;

    @entry def main(args: array<string>) {
        n = 4;
        new sdagtest@array1d(n, n, n, self@);
    }

    @entry def tally() {
        count += 1;

        if (count >= n) {
            println("done");
            exit();
        }
    }
}

class sdagtest {
    val neighbors: array<int, 1>;
    val mainProxy: main@;
    val numIts: int;
    val n: int;

    @entry def sdagtest(=n: int, =numIts: int, =mainProxy: main@) {
        // TODO enable [ (idx + n - 1) % n, (idx + 1) % n ]
        val idx = self[@]index();
        neighbors = new array<int, 1>(2);
        neighbors[0] = (idx + n - 1) % n;
        neighbors[1] = (idx + 1) % n;
        self[@]run();
    }

    @mailbox def receive(it: int, from: int);

    @threaded @entry def run() {
        // TODO shorten self[@]index()
        val idx = self[@]index();

        for (var it = 0; it < numIts; it += 1) {
            for (var i = 0; i < neighbors.size(); i += 1) {
                self@[neighbors[i]].receive(it, idx);
            }
        }

        for (var it = 0; it < numIts; it += 1) {
            for (var i = 0; i < neighbors.size(); i += 1) {
                when receive(_ == it, _ == neighbors[i]) => {
                    println(`[${idx}] received value from ${neighbors[i]} for ${it}.`);
                }
            }
        }

        mainProxy.tally();
    }
}
