package examples;

import ergoline::_;
import ck;

@main class main {
    @entry def main() {
        val n = ck::numPes() * 4;
        new sdagtest@array1d(n, n, n * 4, self@);
    }

    @entry def done(count: int) {
        println(`done with count ${count}`);
        exit();
    }
}

class sdagtest {
    val neighbors: array<int, 1>;
    val mainProxy: main@;
    val numIts: int;
    val n: int;

    @entry def sdagtest(=n: int, =numIts: int, =mainProxy: main@) {
        // TODO enable [ (idx + n - 1) % n, (idx + 1) % n ]
        val idx = self[@]index();
        neighbors = new array<int, 1>(2);
        neighbors[0] = (idx + n - 1) % n;
        neighbors[1] = (idx + 1) % n;
        self[@]run();
        self[@]poke();
    }

    @mailbox def fake();
    @mailbox def poke();
    @mailbox def receive(it: int, from: int);

    @threaded @entry def run() {
        // TODO shorten self[@]index()
        val idx = self[@]index();

        await any {
            when poke() => println(`[${idx}] has been poked!`);
            when fake() => abort("you shouldn't see this!");
        }

        for (var it = 0; it < numIts; it += 1) {
            for (var i = 0; i < neighbors.size(); i += 1) {
                self@[neighbors[i]].receive(it, idx);
            }
        }

        for (var it = 0; it < numIts; it += 1) {
            for (var i = 0; i < neighbors.size(); i += 1) {
                when receive(_ == it, _ == neighbors[i]) => {
                    println(`[${idx}] received value from ${neighbors[i]} for ${it}.`);
                }
            }
        }

        self[@]contribute(idx, int::plus, mainProxy.done);
    }
}
