package examples;

import ergoline::_;
import ck;

@main class main {
    @entry def self() {
        (new nodes@nodegroup(new workers@group(self@))).send();
    }

    @entry def done(i: int) {
        assert(i == ck::numPes());

        println("main> done~!");

        exit();
    }
}

class nodes {
    private val w: workers@group;

    @entry def self(=w: workers@group) {}

    @entry def send() {
        val mine = self[@]index();
        val start = ck::nodeFirst(mine);
        val end = start + ck::nodeSize(mine);

        println(`nd${mine}> sending a message to PEs [${start}, ${end}).`);

        for (var i = start; i < end; i += 1) {
            w[i].recv(mine);
        }
    }
}

class workers {
    val m: main@;

    @entry def self(=m: main@) {}

    @entry def recv(i: int) {
        assert(i == ck::myNode());

        println(`pe${ck::myPe()}> got the message~!`);

        self[@]contribute(1, int::+, m.done);
    }
}
